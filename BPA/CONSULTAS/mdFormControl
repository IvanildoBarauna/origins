Attribute VB_Name = "mdFormControl"
Option Explicit

Public Sub SaveOnListObject()
Dim oFicha As Ficha

Set oFicha = New Ficha
    
    With oFicha
        .Profissional = frmLançamentos.cbo_prof.Value
        .DataNascimento = frmLançamentos.txt_nascto.Value
        .DataInicial = frmLançamentos.txt_databpa.Value
        .Save
    End With
End Sub

Public Function ValidateEmptyFields(FRM As MSForms.UserForm) As Boolean
Dim xField As MSForms.control

ValidateEmptyFields = False

    For Each xField In FRM.Controls
        If VBA.TypeName(xField) = "TextBox" Or VBA.TypeName(xField) = "ComboBox" Then
            If xField.Value = vbNullString Then ValidateEmptyFields = True
        End If
    Next xField
End Function

Public Sub ClearFields(FRM As MSForms.UserForm)
Dim xField As MSForms.control

    For Each xField In FRM.Controls
        If VBA.TypeName(xField) = "TextBox" Or VBA.TypeName(xField) = "ComboBox" Then
            If xField.Name = "txt_databpa" Then GoTo NextControl
            xField.Value = vbNullString
        End If
NextControl:
    Next xField
End Sub

Public Sub FRMPRINT_BTNPRINT()
Dim ws      As Worksheet
Dim pTable  As PivotTable
Dim fDialog As FileDialog
Dim xPath   As String

Application.ScreenUpdating = False

Set ws = shDyn
Set pTable = ws.PivotTables("dyn_bpa")
Set fDialog = Application.FileDialog(msoFileDialogFolderPicker)

    With pTable
        .PivotFields("YEAR").ClearAllFilters
        .PivotFields("MONTH").ClearAllFilters
        .PivotCache.Refresh
        .PivotFields("YEAR").CurrentPage = frmPrint.txt_ano.Value
        .PivotFields("MONTH").CurrentPage = frmPrint.cbo_mês.Value
    End With
    
    With fDialog
        .Title = "Escolha o local para salvar o arquivo PDF"
        .ButtonName = "Salvar aqui"
        If .Show = -1 Then
        xPath = .SelectedItems(1) & Application.PathSeparator & _
            VBA.Replace(ThisWorkbook.Name, ".xlsm", "")
            With ws
                .Visible = True
                .ExportAsFixedFormat Type:=xlTypePDF, Filename:=xPath, Quality:=xlQualityStandard, IgnorePrintAreas:=True, OpenAfterPublish:=True
                .Visible = False
                 Unload frmPrint
            End With
        Else
            MsgBox "Nenhum local foi selecionado, operação cancelada", vbExclamation
        End If
    End With
Application.ScreenUpdating = True
End Sub
