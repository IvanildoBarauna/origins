Attribute VB_Name = "ETL"
Sub ETL()
    On Error GoTo msg
    Dim sArquivo        As String
    
    sArquivo = fxStrFile()
    
    If VBA.LCase(sArquivo) Like "*resumo*" And VBA.Right(sArquivo, 4) = ".pdf" Then
        wsResumo.Range("A2").Value2 = sArquivo
        ThisWorkbook.RefreshAll
                
        MsgBox "Importação realizada com sucesso, os dados estão sendo atualizados e nos próximos segundos estarão disponíveis para exportaçao.", vbInformation
    Else
        MsgBox "Nenhum arquivo foi selecionado ou o arquivo selecionado está incorreto, por favor repita o processo selecionando o arquivo de resumo em PDF.", vbCritical, "Operação Abortada"
    End If

    Exit Sub
    
msg: MsgBox "Erro na execução do ETL, tente novamente ou contate o desenvolvedor.", vbCritical
    Debug.Print vbNewLine & "ETL" & VBA.Err.Description & " em:" & VBA.Now()
End Sub

Function fxStrFile() As String
    On Error GoTo msg
    Dim fd              As FileDialog
    Dim FileName        As String
    
    Set fd = Application.FileDialog(msoFileDialogFilePicker)
    
    With fd
        .Title = "Selecionar o arquivo de resumo em PDF"
        .ButtonName = "Importar"
        .Filters.Add "Arquivos PDF", "*.pdf"
        .FilterIndex = 2
        .AllowMultiSelect = False
        If .Show <> -1 Then Exit Function
        FileName = .SelectedItems(1)
    End With
    
    fxStrFile = FileName
    
    Exit Function
    
msg: MsgBox "Erro na execução da rotina fxstrFile, tente novamente ou contate o desenvolvedor.", vbCritical
Debug.Print vbNewLine & "fxStrFile" & VBA.Err.Description & " em:" & VBA.Now()
End Function

Function fxStrFolder() As String
    On Error GoTo msg
    Dim fd                  As FileDialog
    Dim FolderName          As String
    Dim fso                 As Object 'Scripting.FileSystemObject
    
    Set fso = VBA.CreateObject("Scripting.FileSystemObject")
    'New Scripting.FileSystemObject
    
    Set fd = Application.FileDialog(msoFileDialogFolderPicker)
    
    With fd
        .Title = "Selecionar a pasta para exportação dos arquivos"
        .ButtonName = "Selecionar esta pasta"
        .AllowMultiSelect = False
        If .Show <> -1 Then Exit Function
        FolderName = .SelectedItems(1) & "\ArquivosExportados"
    End With
    
    If Not fso.FolderExists(FolderName) Then fso.CreateFolder (FolderName)
    
    fxStrFolder = FolderName

    Exit Function
    
msg:
    MsgBox "Erro na execução da rotina fxStrFolder, tente novamente ou contate o desenvolvedor.", vbCritical
    Debug.Print vbNewLine & "fxStrFolder" & VBA.Err.Description & " em:" & VBA.Now()
End Function




