Attribute VB_Name = "Execuções"
Sub GeneralExecution()
'----------------------------------------
'PROJETO: Extração de PDF para envios através de arquivos
'OBJETIVO: Realizar a extração dos arquivos PDF e imagem da planilha de colaborador
'para pastas com o nome de cada gerente.
'DATA: 27/06/2021 21:25
'DESENVOLVEDOR: IVANILDO BARAUNA DE SOUZA JUNIOR
'EMAIL: ivanildo.jnr@outlook.com
'----------------------------------------
    On Error GoTo msg
    Dim arrGerentes         As Variant
    Dim loParam             As ListObject
    Dim iGerente            As Integer
    Dim loResume            As ListObject
    Dim fso                 As Object 'Scripting.FileSystemObject
    Dim strfoldergerente    As String
    Dim rngVisible          As Range
    Dim irng                As Range
    Dim strFolderName       As String
    Dim pctDone             As String
    Dim pctDoneColab        As String
    Dim arrColabs           As Variant
    Dim iColab              As Integer
    Dim pTable              As PivotTable
    
    Application.ScreenUpdating = True
    strFolderName = fxStrFolder()
    
    If strFolderName = "" _
        Then MsgBox "Nenhuma pasta foi selecionada, selecione uma pasta para exportação dos arquivos.", vbExclamation: Exit Sub
    
    If (MsgBox("O processo a seguir pode demorar 5 minutos ou mais, neste período o computador deverá ficar inativo, deseja realmente continuar?", vbYesNo + vbQuestion, "Deseja continuar?")) _
        = vbNo Then Exit Sub
    
    Set pTable = wsExportGerente.PivotTables("rptGerente")
    pTable.PivotCache.Refresh
    ThisWorkbook.Save
    wsCardColab.Select
    
    Set fso = VBA.CreateObject("Scripting.FileSystemObject")
    Set loParam = wsParam.ListObjects("ParamGerente")
    Set loResume = wsResumo.ListObjects(1)
    
    ReDim arrGerentes(1 To loParam.ListRows.Count)
    
    For iGerente = 1 To UBound(arrGerentes)
        arrGerentes(iGerente) = loParam.ListColumns(1).DataBodyRange(iGerente).Value2
    Next iGerente
    
    
    For iGerente = 1 To UBound(arrGerentes)
        strfoldergerente = strFolderName & "\GERENTE-" & arrGerentes(iGerente)
        If Not fso.FolderExists(strfoldergerente) Then fso.CreateFolder (strfoldergerente)
        
        wsExportGerente.Range("B4").Value2 = arrGerentes(iGerente)
        
        wsExportGerente.ExportAsFixedFormat xlTypePDF, strfoldergerente & _
            "\RESUMO_GERENTE.pdf", OpenAfterPublish:=False
        
        pctDone = VBA.Left((iGerente / UBound(arrGerentes)) * 100, 4) & "%"
        
        wsCardColab.Range("B12").Value2 = _
            "Exportando gerente: " & arrGerentes(iGerente) & VBA.Str(iGerente) & " de" & VBA.Str(UBound(arrGerentes)) & " >>>> " & pctDone & " concluído"
        
        
        loResume.Range.AutoFilter Field:=10, Criteria1:=arrGerentes(iGerente)
        Set rngVisible = loResume.ListColumns(1).DataBodyRange.SpecialCells(xlCellTypeVisible)
        
        ReDim arrColabs(1 To rngVisible.Rows.Count)
        
        iColab = 0
        
        For Each irng In rngVisible
            iColab = iColab + 1
            arrColabs(iColab) = irng.Value2
        Next irng
        
        For iColab = 1 To UBound(arrColabs)
            pctDoneColab = VBA.Left((iColab / rngVisible.Count) * 100, 4) & "%"
            With wsCardColab
                .Range("B13").Value2 = "Exportando colaboradores: " & iColab & " de " & UBound(arrColabs) & " >>>> " & pctDoneColab & " concluído."
                .Range("C1").Value2 = arrColabs(iColab)
                .Rows("7:7").EntireRow.Hidden = Not ValAjudaCusto(arrColabs(iColab))
                .Rows("6:6").EntireRow.Hidden = Not ValComissao(arrColabs(iColab))
            End With
            Call ExportForImage(strfoldergerente & "\" & arrColabs(iColab) & ".jpg")
        Next iColab
        
    Next iGerente
    
    loResume.ShowAutoFilter = False
    
    wsCardColab.Range("C1").Value2 = ""
    wsCardColab.Range("B12").Value2 = ""
    wsCardColab.Range("B13").Value2 = _
        VBA.Now() & " | " & pctDone & " concluído" & " de um total de" _
            & VBA.Str(loResume.ListRows.Count + loParam.ListRows.Count) _
                & " Arquivos >> " & strFolderName
            
    MsgBox "Os arquivos foram exportados com sucesso para a pasta: " & vbNewLine & strFolderName, vbInformation, "Extração Concluída"
    
    Exit Sub
    
msg:
    MsgBox "Erro na execução da rotina GeneralExecution, tente novamente ou contate o desenvolvedor.", vbCritical
    Debug.Print vbNewLine & "GeneralExecution / " & VBA.Err.Description & " em: " & VBA.Now()
End Sub
