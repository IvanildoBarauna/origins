Attribute VB_Name = "frmLançamentos"
Attribute VB_Base = "0{2C1D40EB-70EE-41BA-BA88-9FBE1E26AEDB}{18262588-0388-4F0E-A2D2-82295D7AC409}"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Attribute VB_TemplateDerived = False
Attribute VB_Customizable = False
Option Explicit

Private Sub btn_voltar_Click()
    Unload Me
End Sub

Private Sub chk_otherdate_Click()
    If Me.chk_otherdate.Value Then
        Me.txt_databpa.Enabled = True
        Me.txt_databpa.SetFocus
    End If
End Sub

Private Sub UserForm_Initialize()
    Dim ws As Worksheet
    Dim lo As ListObject
    Dim lr As Integer
    Dim i As Integer
    
    shApoio.Activate
    Set ws = shListas
    Set lo = ws.ListObjects("LISTA_PROCED")
    lr = lo.ListRows.Count + 1
    
    For i = 2 To lr
        cbo_prof.AddItem ws.Cells(i, 1).Value2
    Next i
    
    Me.btnLan.Enabled = False
    Me.txt_databpa = DateSerial(Year(Date), Month(Date) - 1, 21)
    Me.txt_databpa.Enabled = False
    
End Sub
Private Sub cbo_prof_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    With Me
        If .cbo_prof.Value = "" Then
            .lbvalida.Caption = "#ERRO PROF = VAZIO"
            .lbvalida.ForeColor = &HFF&
            .btnLan.Enabled = False
        Else
            .lbvalida = ""
        End If
    End With
End Sub

Private Sub txt_nascto_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    With Me
        If .txt_nascto = "" Then
            .lbvalida.ForeColor = &HFF&
            .lbvalida = "#ERRO DATA NASCTO = VAZIO"
            Cancel = True
            .btnLan.Enabled = False
        ElseIf Len(Replace(.txt_nascto, "/", "")) <> 8 Then
            .lbvalida.ForeColor = &HFF&
            .lbvalida = "#ERRO DATA NASCTO = INVÁIDA."
            Cancel = True
            txt_nascto.Value = ""
        Else
            .lbvalida = ""
            .btnLan.Enabled = True
            .txt_nascto.Value = Format(Replace(Me.txt_nascto.Value, "/", ""), "00\/00\/0000")
        End If
    End With
End Sub

Private Sub txt_databpa_Exit(ByVal Cancel As MSForms.ReturnBoolean)
    With Me
        If .txt_databpa = "" Then
            .lbvalida.ForeColor = &HFF&
            .lbvalida = "#ERRO DATA BPA = VAZIO"
            Cancel = True
            .btnLan.Enabled = False
        ElseIf Len(Replace(.txt_databpa, "/", "")) <> 8 Then
            .lbvalida.Caption = "#ERRO DATA BPA = INVÁLIDA"
            .txt_databpa.Value = ""
            Cancel = True
            .lbvalida.ForeColor = &HFF&
            .btnLan.Enabled = False
        Else
            .lbvalida = ""
            .btnLan.Enabled = True
            .txt_databpa.Value = Format(Replace(.txt_databpa, "/", ""), "00\/00\/0000")
        End If
    End With
End Sub

Private Sub btnLan_Click()
    With Me.lbvalida
        .Caption = vbNullString
        .ForeColor = &HC000&
         SaveOnListObject
        .Caption = "FICHA LANÇADA!"
    End With
    Call ClearFields(Me)
    With Me
        .cbo_prof.SetFocus
        .txt_databpa.Enabled = False
        .chk_otherdate.Value = False
    End With
End Sub
